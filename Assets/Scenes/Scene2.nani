@stopSfx
@stopBgm
...
@sfx AlarmElectonic

@bgm Sc2BirdSinging

@printer Thoughts
Тут так галасно… Можна я ще трохи посплю?
Мені наснилося, ніби я — піддослідна миша.
Мені на голову надягли чи то шолом, чи то обруч, ввели мені в вену якусь рідину. І потім мене трясло як на атракціоні в парку Перемоги.
Вставати не хочеться. Але треба. До того ж сьогодні великий день! Треба файно вдягнутися перед розмовою з шефом.
Вперед, {PlayerName}!

@back Sc2_LivRoom
@choice "Взяти телефон і подивитися новини."
@stop

Стоп.
Мені здається, чи я чую голоси? Хтось сперечається на кухні. Чоловік і жінка.
Хіба тут може бути ще хтось, крім мене?


@printer Voices
Голос1: Навіть якщо це тобі вдалося, такий шалений план може завалитися на будь-якому етапі. Ти забула, що в них половина мозку не працює?


@printer Voices
Голос2: В нас немає іншого виходу, як ти не розумієш!

@printer Voices
Голос1: Я всього лише кажу, що це ненадійний елемент, і що в будь-який момент все може піти псу під хвіст. Я пропоную продумати варіант без цієї людини!

@printer Thoughts
Якого біса? До мене що, вори увірвалися?
Адреналін шумить в моїй крові. Думки скачуть, як шалені вивірки. Мозок підкидає варіанти ситуацій, і я не знаю, на який відгукнутися.

@choice "Мені лячно. Треба завмерти." goto:.Ch1Option1
@choice "Я зараз їх повбиваю, нахабних інтервентів!!" goto:.Ch1Option2
@stop 

#Ch1Option1
Сховаюся під ковдрою і буду лежати тихо як миша.
@goto .Fall

#Ch1Option2
Тримайте мене семеро!
@goto .Fall

#Fall

@sfx Falling

Я падаю, перекочуюся, всі мої м'язи напружуються, кінцівки роблять ряд дивних стрибучих рухів, і я опиняюся посеред кімнати. От незграба.
@back Sc2_LivRoom_2

@bgm ReincarnationTheme2

@char Paw.def

Що?! В мене лапки?
О, тоді це все пояснює!

@hide Paw

@printer Voices
Голос2: Слухай, я тебе знаю, ти боїшся всього нового, але як раніше вже не буде. Ми в біді, ми мусимо це зробити.

@printer Thoughts
Що вона вбіса верзе? Що взагалі відбувається?
Точно, це все — сон! І все довкола — результат роботи мого мозку.
Сон, в якому до мене увірвалися вори і при цьому я — котик.

@printer Voices
Голос2: Ми вже почали робити з нашого чолтіні воїна справедливості. І ми доведемо це до кінця!

@printer Thoughts
Про кого вони говорять?
Про мене, більше нікого немає в цій квартирі.
А може то як в старому фільмі — хтось переплутав квартири?
Хоча ні, я зачиняв двері.
То як вони мене назвали? Вони хочуть зробити з мене свого — кого?
Воїна справедливості???!

@printer Voices
Голос1: Я не хочу повторювати помилки Сутінок, я просто не хочу опинитися за гратами!

@printer Voices
Голос2: Яке ж ти сцикло.

@printer Thoughts
Так. Годі чекати — мова йде про кримінальні злочини, до того ж я можу запізнитися на роботу. 
Що б вони там собі не вигадали — я маю себе захистити. Треба зробити хоч щось.
Блін, в мене лапки. І я не знаю, як ними управляти. А ззовні котики виглядають такими елегантними!
Кімната така велика і сповнена нових ароматів. Логічно, адже у котів гарний нюх.
Запахи нагадують історії.

@choice "Принюхатися"
@stop 

@sfx Exhale

Недочитана книжка, вуличний пил, постіль, немита газова плита на кухні.
Парфуми жінки, яка пройшла повз вікно, бутерброд, що чекає на мене.
Килим… Хіба килим має свій запах?!
Пахне шерстю, людськими ногами, розлитим пивом, пральним засобом, лампою, масажним маслом, слиною і ще чимось…
Вишуканий букет.

@char Night.smile pos:75,0
@printer Night
Ніч: Мені вдалося, День! Я трансформувала чолтіні на роніксоні.

@printer Dialogue
Що? Моя Дряпа розмовляє? І що це на ній?

@char Day.def pos:30,0
@printer Day
День: Привіт, {PlayerName}!

@printer Dialogue
{PlayerName}: Пушок?

@printer Thoughts
Оце так сон.

@printer Day
@char Day.meh
День: Який я тобі Пушок?! Ніч, я тобі говорив — в них часу на мислення йде більше, ніж у нас на те, щоб виспатися.

@printer Night
@char Night.def
Ніч: Все добре, День — його мати не співала йому ронвітів, на відміну від твоєї. 

@printer Night
@char Night.angry
Ніч: Незважаючи на те, що ти бачиш перед собою роніксоні, він все ще людина. Тож тримай будь ласка свого язика за зубами.

@printer Dialogue
{PlayerName}: Ви ж мої котики, так?

@printer Day
@char Day.interesting
День: Бачиш, Ніч, ми для нього милі котики, навіть коли він бачить нас справжніми! Люди люблять нас, ми їхні котики.

@printer Night
@char Night.angry
Ніч: Звільни мої вуха від того слова.

@printer Dialogue
{PlayerName}: Від якого слова?

@printer Night
@char Night.def
Ніч: На букву "к".

@printer Day
@char Day.blink
День: Не звертай уваги, вона така чутлива, бо занадто довго займалася революцією.

@printer Thoughts
Пушок і Дряпа виглядають розгубленими. Який все-таки дивний сон.
Треба якось дізнатися більше про цей сомнамбулічний світ… О, є ідея.

@printer Dialogue
{PlayerName}: "Ви — лише плоди моєї сплячої фантазії. Ви несправжні."

@printer Day
@char Day.meh
День: Власне кажучи, насправді єдиний "плід" тут ти.

День: Всьому свій час. Скоро ти про все дізнаєшся. Але є одне але...

@printer Night
@char Night.angry
Ніч: Так, в нас є умови. Ти маєш нас поважати, а не ставитися як до немовлят.

@choice "Це ж ваших рук справа, так? То ви мені щось поясните?"
@stop

@printer Night
@char Night.thinking
Ніч: Так, слухай уважно...

@printer Day
@char Day.surprised
День: Стоп. Ніч, подивись на це. Перед нами людина, яка намагається тобі підіграти, а насправді впевнена, що спить.

День: В такому стані люди, тобто чолтіні, не вміють довіряти. Ти права, нам треба бути розумніше. 

@printer Day
@char Day.blink
День: Коли ти, Ніч, не вміла ходити, тобі пояснювали доленосні речі? Ні-ні-ні, ми не можемо довірити нашу місію шматочку розплавленого сира.

@printer Thoughts
Слід визнати, він правий. Впавши з кроваті, я так і лежу на килимі, вдихаючи його аромати. А як піднятися, й гадки не маю.

@printer Night
@char Night.thinking
Ніч: І що ти пропонуєш робити?

@printer Day
@char Day.def
День: Просто почекаємо, доки звикне, освоїться зі своїм тілом і зрозуміє, що це не сон.

@choice "Все це сон."
@choice "Це не сон."
@stop

@printer Night
@char Night.smile
Ніч: Зараз просканувала чолтіні через медичне розширення. І знаєш що, ти правий.

Ніч: Життєві показники свідчать про високий ризик брехні та необдуманих вчинків.

@printer Dialogue
{PlayerName}: То ви хочете мене викрасти в цьому сні?

@printer Day
@char Day.surprised
День: Що?

@printer Dialogue
{PlayerName}: Ви говорили про це на кухні…

@printer Day
@char Day.meh
День: Мушу визнати, що роніксонська назва для вашого виду — чолтіні, тобто, "відкривашка для консервів"  —  ідеально тобі підходить.

@printer Night
@char Night.interesting
Ніч: Я трансмодифікувала твоє чолтінське тіло на тіло роніксоні для того, щоб поговорити з тобою.

@printer Dialogue
{PlayerName}: Он воно як...

@printer Night
@char Night.smile
Ніч: Ти — частина нашої місії.

@printer Day
@char Day.blink
День: Так-так. Але для початку тобі, {PlayerName}, треба усвідомити одну річ.

@printer Thoughts
Він що, підморгує мені?

@printer Night
@char Night.def
Ніч: Що це не сон, а ми у жодному разі не коти.

@printer Day
@char Day.misunderstood
День: Власне, це дві речі. Та й я мав на увазі інше. А саме, те, що мене звуть День, а не "Пушок". А вона — Ніч, а не "Дряпа".

@printer Dialogue
{PlayerName}: О ні. Вам не подобаються прізвиська, вигадані мною…

@printer Night
@char Night.def
Ніч: Я розповім тобі про твою місію.

@printer Day
@char Day.blink
День: Так, вона розповість. Але згодом, коли ти очухаєшся.

@printer Night
@char Night.thinking
Ніч: Тільки швидше, будь ласка, ти й не уявляєш…

@printer Day
@char Day.surprised
День: Ніченько, давай дамо нашій людині трохи часу. До вечора ще довго.

@printer Night
@char Night.def
Ніч: Добре. Тоді ми поки що підемо. В тебе є п'ятнадцять хвилин на те, щоб остаточно прокинутися, навчитися ходити та відчути себе тим, ким став.

Ніч: Ми будемо на кухні — приходь, як будеш готовий.

@printer Day
@char Day.blink
День: Звертайся, друже.

@hideChars

@choice "Що б вони там не казали, це все-таки сон." goto:.Ch2Option1
@choice "А якщо все це правда?" goto:.Ch2Option2
@stop

#Ch2Option1
    @printer Thoughts
    Чудернацький веселий сон, і він тільки почався.
    @goto .Ch2_Continue

#Ch2Option2
    @printer Thoughts
    І насправді коти — такі?
    Роніксоні. А люди — чолтіні.
    @goto .Ch2_Continue

#Ch2_Continue

Час дослідити це все.

@choice "Застрибнути на стіл" goto:.Ch3Option1
@choice "Залізти під ліжко" goto:.Ch3Option2
@stop
#Ch3Option1
    Звідти видніше.
    Цікава ідея.
    Встати з підлоги виходить не одразу. Геть не зрозуміло, як рухати чотирма лапами, які прикріплені до тіла інакше.
    Мій хребет продовжується поза тілом, і він чомусь увесь час рухається, варто мені лише замислитися про щось. Здається, він відображає емоції.
    І я весь час відчуваю безліч запахів. Все це дуже відволікає
    Намагаючись піднятись на ноги, точніше на задні лапи, я втрачаю рівновагу і падаю.

    @choice "Спробувати ще раз"
    @stop

    Мені вже значно зручніше стояти на двох задніх лапах. Так… Але застрибнути на стіл — це надто складна операція для мене зараз.
    @goto .StepsStart

#Ch3Option2
    @sfx Exhale

    Звідти пахне чимось цікавим.
    Цікава ідея.
    Встати з підлоги виходить не одразу. Геть не зрозуміло, як рухати чотирма лапами, які прикріплені до тіла інакше.
    Мій хребет продовжується поза тілом, і він чомусь увесь час рухається, варто мені лише замислитися про щось. Здається, він відображає емоції.
    І я весь час відчуваю безліч запахів. Все це дуже відволікає.
    Намагаючись зігнути всі чотири лапи, щоб зазирнути під ліжко, я не даю раду своїм гнучким суглобам і завалююся на бік.

    @choice "Спробувати ще раз"
    @stop

    Мені вже зручніше стояти на двох лапах. Так… Але залізти під ліжко — це надто складна операція для мене зараз.
    @goto .StepsStart

#StepsStart
Треба спочатку освоїтися в цьому тілі.

Ліва лапа, правий хвіст, задня передня… Ой як тут легко заплутатися.

З чого ж мені почати?
; ПОЧАТОК КВЕСТУ З ЛАПАМИ!!!!!!!

@goto .Step1

#Step1
Перший крок.

@choice "Права задня лапа" goto:.RightBackOne
@choice "Ліва передня лапа" goto:.LeftFrontOne
@choice "Ліва задня лапа" goto:.LeftBackOne
@choice "Права передня лапа" goto:.RightFrontOne
@stop

#RightBackOne
Добре, яким буде другий крок?
@choice "Ліва передня лапа" set:stepOne="RightBackLeftFront" goto:.Step2
@choice "Ліва задня лапа" goto:.FailBack
@choice "Права передня лапа" goto:.FailSide
@stop

#LeftFrontOne
Поки що виходить. Якою лапою ступати далі?
@choice "Права задня лапа" set:stepOne="LeftFrontRightBack" goto:.Step2
@choice "Ліва задня лапа" goto:.FailSide
@choice "Права передня лапа" goto:.FailFront
@stop

#LeftBackOne
Перша лапа є, що далі?
@choice "Права задня лапа" goto:.FailBack
@choice "Ліва передня лапа" goto:.FailSide
@choice "Права передня лапа" set:stepOne="LeftBackRightFront" goto:.Step2
@stop

#RightFrontOne
Так, тепер наступна лапа...
@choice "Права задня лапа" goto:.FailSide
@choice "Ліва передня лапа" goto:.FailFront
@choice "Ліва задня лапа" set:stepOne="RightFrontLeftBack" goto:.Step2
@stop

; Fail states

#FailSide
Щось не те. Чому мене тепер клонить убік? Треба повернутись у початкову позицію і спробувати інший підхід.
@goto .Step1

#FailFront
Моя передня частина суне далі, а задня так і продовжує стояти. Ще трохи, і розтягнуся пузом по підлозі. Спробуємо ще раз.
@goto .Step1

#FailBack
Тьху, якщо рухати тільки обома задніми, одразу плюхаюсь на п'яту точку. Треба спробувати іншу послідовність.
@goto .Step1

#Fail
І все ж таки я десь помилився... як же це складно!
@sfx Fell

@goto .Step1

; ПОЧАТОК ДРУГОЇ КОМБІНАЦІЇ КРОКІВ

#Step2
Ось так! Треба чергувати. Лапи якимось чином приєднані в цьому тілі діагонально
Неперевершено! Цілих два котячих кроки! Але лапи чотири, і всі їх треба якось задіяти. Тож…
Третій крок

@choice "Права задня лапа" goto:.RightBackCheck
@choice "Ліва передня лапа" goto:.LeftFrontCheck
@choice "Ліва задня лапа" goto:.LeftBackCheck
@choice "Права передня лапа" goto:.RightFrontCheck
@stop

#RightBackCheck
@if stepOne=="RightBackLeftFront"
	Тьху ти! Знову щось наплутав. Я ж уже ступав цією лапою! Доведеться починати спочатку...
	@goto .Step1
@elseif stepOne=="LeftFrontRightBack"
	Знову щось не те. Я вже задіяв лапу з цієї пари. Почнемо спочатку.
	@goto .Step1
@elseif stepOne=="LeftBackRightFront"
	Я відчуваю дивне естетичне задоволення! Залишилася остання лапа.
	@goto .RightBackTwo
@elseif stepOne=="RightFrontLeftBack"
	Вау, клас! Я майже вмію ходити як кіт! І остання лапа…
	@goto .RightBackTwo
@endif

#LeftFrontCheck
@if stepOne=="RightBackLeftFront"
	Тьху ти! Знову щось наплутав. Я ж уже ступав цією лапою! Доведеться починати спочатку...
	@goto .Step1
@elseif stepOne=="LeftFrontRightBack"
	Знову щось не те. Я вже задіяв лапу з цієї пари. Почнемо спочатку.
	@goto .Step1
@elseif stepOne=="LeftBackRightFront"
	Я відчуваю дивне естетичне задоволення! Залишилася остання лапа.
	@goto .LeftFrontTwo
@elseif stepOne=="RightFrontLeftBack"
	Вау, клас! Я майже вмію ходити як кіт! І остання лапа…
	@goto .LeftFrontTwo
@endif

#LeftBackCheck
@if stepOne=="RightBackLeftFront"
	Вау, клас! Я майже вмію ходити як кіт! І остання лапа…
	@goto .LeftBackTwo
@elseif stepOne=="LeftFrontRightBack"
	Я відчуваю дивне естетичне задоволення! Залишилася остання лапа.
	@goto .LeftBackTwo
@elseif stepOne=="LeftBackRightFront"
	Тьху ти! Знову щось наплутав. Я ж уже ступав цією лапою! Доведеться починати спочатку...
	@goto .Step1	
@elseif stepOne=="RightFrontLeftBack"
	Знову щось не те. Я вже задіяв лапу з цієї пари. Почнемо спочатку.
	@goto .Step1	
@endif

#RightFrontCheck
@if stepOne=="RightBackLeftFront"
	Вау, клас! Я майже вмію ходити як кіт! І остання лапа…
	@goto .RightFrontTwo
@elseif stepOne=="LeftFrontRightBack"
	Я відчуваю дивне естетичне задоволення! Залишилася остання лапа.
	@goto .RightFrontTwo
@elseif stepOne=="LeftBackRightFront"
	Знову щось не те. Я вже задіяв лапу з цієї пари. Почнемо спочатку.
	@goto .Step1	
@elseif stepOne=="RightFrontLeftBack"
	Тьху ти! Знову щось наплутав. Я ж уже ступав цією лапою! Доведеться починати спочатку...
	@goto .Step1
@endif

#RightBackTwo
Четвертий крок.
@choice "Ліва передня лапа" goto:.StepsCompleted
@choice "Ліва задня лапа" goto:.Fail
@choice "Права передня лапа" goto:.Fail
@stop

#LeftFrontTwo
Четвертий крок.
@choice "Права задня лапа" goto:.StepsCompleted
@choice "Ліва задня лапа" goto:.Fail
@choice "Права передня лапа" goto:.Fail
@stop

#LeftBackTwo
Четвертий крок.
@choice "Права задня лапа" goto:.Fail
@choice "Ліва передня лапа" goto:.Fail
@choice "Права передня лапа" goto:.StepsCompleted
@stop

#RightFrontTwo
Четвертий крок.
@choice "Права задня лапа" goto:.Fail
@choice "Ліва передня лапа" goto:.Fail
@choice "Ліва задня лапа" goto:.StepsCompleted
@stop

#StepsCompleted
Ура! Вдалося! Тепер я вмію ходити на чотирьох лапах.

@choice "Пройтися по кімнаті"
@stop

@sfx TwoFriendlyMeow
@toast "Досягнення: Котяча Хода"

Я роблю свої перші кроки дуже повільно. Хвіст незадоволено б'ється з боку в бік, розхитує тіло. Доводиться напружувати мозок.
Але згодом мої рухи стають все більш впевненими.
То ж тепер можна зробити те, чого мені від початку цього сна хотілося:

@set table=0
@set bed=0
@goto .WhatToDo

#WhatToDo
@if table>=1&&bed>=1
    @goto .QuestsDone
@else 
    @choice "Застрибнути на стіл" if:table=0 goto:.Table set:table=table+1
    @choice "Залізти під ліжко" if:bed=0 goto:.Bed set:bed=bed+1
    @stop

        #Table

            Я встаю на задні лапи і відштовхую підлогу від себе. Час уповільнюється, мить стає вічністю.
            Тіло немов розтиснута пружина вистрілює вгору, я встигаю роздивитися речі на столі: ноутбук, комп'ютерна миша, стакан води, фотографія в рамці.
            Обираю, де розміститися, і моя передня частина тіла тягне за собою задню, яка повторює рух голови та передніх лап по параболі.

            @sfx PawsWood

            І ось вже я весь пружно приземлився на власний стіл, стою на ньому, гордий собою.
            І як мені це вдалося?
            В моєму тілі — сила десятьох еспресо, випитих за один день.

            @choice "Роззирнутися."
            @stop
            
            Я бачу світ за вікном, відчуваю сотні ароматів, чую тисячі голосів і кроків.

            @set closet=0

            #WhatToDoTable
            Придивившись, я бачу нові можливості
                @if table>0&&closet>0
                    @goto .WhatToDo
                @else
                    @choice "На столі." if:table=1 goto:.OnTheTable set:table=table+1
                    @choice "На шафі" if:closet=0 goto:.Closet set:closet=closet+1
                    @stop

                    #OnTheTable
                        Помічаю свій ноутбук. Відкритий.

                        @printer Dialogue
                        {PlayerName}: Масажне крісло!

                        @choice "Полежати на клавішах ноутбука."
                        @stop

                        ; @sfx *Хтось ліг на клавіші ноута. або нажав їх*

                        О, тут так тепло та м'яко, клавіші втішно нажалися під моєю вагою.

                        @set stay=0
                        @set lay=0

                        #StayOrLay
                        @choice "Встати." if:stay=0 goto:.Stay set:stay=stay+1
                        @choice "Продовжувати лежати." if:lay=0 goto:.Lay set:lay=lay+1
                        @stop

                            #Stay
                            Я встаю з клавіш свого ноутбуку, відчуваючи внутрішню наповненість.
                            @goto .TableGetUp
                            
                            #Lay
                            І чому я тільки додумався до цього? Ідеально ж.
                            Добре, що ноутбук відкритий!

                                #StayOrLay2
                                @choice "Встати." goto:.Stay2
                                @choice "Продовжувати лежати." goto:.Lay2
                                @stop

                                #Stay2
                                Встаю зі свого ноутбуку, відчуваючи себе завойовником клавіатури.
				@sfx TwoFriendlyMeow
				@toast "Досягнення: Завойовник Клавіатури"
                                @goto .TableGetUp

                                #Lay2
                                Я лежу, а розслаблена свідомість повільно рухається в едем дзен-щастя.
                                Моя? Не моя? Просто свідомість. Пливе собі та й пливе.
                                @sfx Purr9
				В глибині горла зароджується дивний низький звук…
				... ... ...
				@sfx TwoFriendlyMeow
				@toast "Досягнення: Клавіатурний Мантромурк"
                                
	
                                Всі клавіші облежані, всі чакри пропрацьовані.
                                @choice "Встати." goto:.TableGetUp
                                @stop

                            #TableGetUp
                            Встаючи, я ледь не роняю хвостом зі стола комп'ютерну мишку, але мені щастить — вона не падає.
                            О, здається, клавіші відбилися на моїй шерсті!

                            Якийсь рух за вікном привертає мою увагу. І це не автівка і не перехожий.
                            Перестрибнувши на підвіконня, я витріщаюся з вікна другого поверху вниз, і помічаю дивного рудого котика, схожого, скоріше, на торговця рішалу з 90-х.
                            Насправді він схожий на сусідського кота Жору. Насправді схоже, це сусідський кіт Жора. Але зараз він трохи більш нарядний, ніж зазвичай.
                            Жора намагається застрибнути на дерево, але чомусь кожного разу сповзає вниз. В нього нічого не виходить.
                            Крізь відчинене вікно я чую.

                            @printer Chaos
                            Жора: Моя кльоцка! Моя кльоцонька!

                            @printer Dialogue
                            {PlayerName}: Що ти кажеш?

                            @printer Chaos
                            Жора: Сусідка з 4 поверху кинула мені кльоцку з вікна, і вона застрягла на дереві. В мене кігті обрізані, я…

                            @printer Dialogue
                            {PlayerName}: …

                            @printer Chaos
                            Жора: Кльоцонька моя люба! Я так хотів тебе пожерти!

                            Жора: Думаю, ти мені нічим не допоможеш — це просто доля.

                            Жора: Кльоцку з'їло дерево. Піду знайду собі іншу кльоцку.

                            @printer Thoughts
                            Жора нервово сьопає хвостом, йде від дерева геть і зникає в під'їзді. Я ще якийсь час роздивляюся вулицю з другого поверху.
                            До забору напроти притулений чийсь ровер. Вздовж стіни будинку тягнеться полоска тротуару. Далі дерева. Ще далі дорога.
                            Йдуть люди, іноді проїзжають одна-дві автівки. На дереві, де застрягла кльоцка, майоріє крихітна біла пляма.
                            На тій гілці, що вказує на північ.
                            ...
                            @goto .WhatToDoTable

            #Closet
                З шафи звисає мотузка, з якою колись любив гратися Пушок. Я тоді вистроїв цілу гіпотезу: Пушок уявляє, що ловить змію. Тому назвав її "зміюка".
                Згодом Пушок знайшов собі інші іграшки, і я закинув її на шафу.
                Але здається, її звідти хтось намагався дістати…
                @goto .WhatToDoTable

        #Bed
            Присідаю на чотири лапи, відчуваючи силу суглобів та жагу пізнання.
            Але дошка бортику ліжка значно нижче, і щоб опинитися в загадковому світі під ліжком, треба якось пролізти під цією штукою.
            Я засовую голову вперед, в пильну шерстяну темряву, і відчуваю запах пригод.
            Штовхаю сильними задніми лапами своє тіло, і воно трохи розплющується по підлозі, немов пластилін.
            І ось я всередині. Чудова гнучкість, просто чудова!
            Довкола мене якийсь мотлох. О, то це країна загублених речей!
            То он куди поділися всі кришки від пляшок і всі іграшкові миші. Дві стогривневі купюри, чотири шурупи, а ще якась химерна смердюча штука, до якої мені чомусь не хочеться наближатися.
            Але з усього речі привертають мою увагу. Мене охоплює бажання…

            @set juice=0
            @set thing=0
            #JuiceOrThing
                @if juice>0&&thing>0
                    @goto .WhatToDo
                @else
                    @choice "Полювати на пластикову трубочку для сока" if:juice=0 goto:.HuntJuice set:juice=juice+1
                    @choice "Витягнути на світ божий ту дивну довгасту штуку в вакуумній упаковці" if:thing=0 goto:.HuntThing set:thing=thing+1

                    @stop

                        #HuntJuice
                            
                            Я дію як велять мені інстинкти. Спочатку ховаюся, зливаючись із середовищем, і стаю самою темрявою.
                            Потім поволі переступаю з однієї задньої лапи на іншу, поки не стане сміливості кинутися на здобич.
                            Нарешті, вистрілюю всім тілом на пластикову трубочку, і так сильно, що опиняюсь поза межами підліжков'я.
                            Трубочка вислизає з-під моїх лап, забавно котиться, і завмирає аж десь під шафою з одягом.

                            @goto .JuiceOrThing

                        #HuntThing
                            Я беру дивну кракозябру в упаковці зубами, і витаскую на світло. Пахне насправді чимось знайомим.

                            @sfx ManMmm

                            І виглядає знайомо. Але з напівстертих залишків малюнку та надписів важко скласти уявлення.
                            Ну й добре! Тепер мені знов треба під ліжко. 
                            @goto .JuiceOrThing
                            ;Гравець повертається в попереднє меню). Якщо вже полював на шуруп: йдемо далі по тексту.

#QuestsDone

    Що ж, тепер я краще знаю цю кімнату. Свою кімнату.
    Чи може, це не моя кімната? 
    Не можу почухати спину, потрібен ще хтось.
    Мабуть треба вже йти на кухню до котів. Чи як там вони себе називали?
    Роніксоні.

    @choice "Іти на кухню"
    @stop

    Рухаюсь до дверей, але в самий відповідальний момент мене наздоганяє дивна думка:
    Чомусь хочеться вийти, але не хочеться вийти.
    Тут, між кімнатами — ідеальне місце, щоб просто стояти. Ніби рішення піти і прийнято, і ще не прийнято водночас.

    @choice "Постою ще трохи."
    @stop

    Я і там і тут. В самому серці паузи. Тут добре.
    @sfx TwoFriendlyMeow
    @toast "Досягнення: Квантовий Кіт"

    @choice "Піду далі."
    @stop

    Я сама рішучість.
    Все, можна йти.

    @stopBgm
    @goto Scene3
