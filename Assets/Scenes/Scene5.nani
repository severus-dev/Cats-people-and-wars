@unlock CG/Sc5_Outside1_Tree
@unlock CG/Sc5_Outside2_1_TWOvasons
@unlock CG/Sc5_Outside2_2_ONEvason
@unlock CG/Sc5_Outside3_inBushes
@stopBgm

@sfx HeartPalpitation loop:true
@back Sc5_Outside1_Tree

@bgm Street

@printer Thoughts
Ні, не на тротуар — я зависаю на дереві, тримаючись усіма чотирма лапами головою вниз.

Дивно, але це досить зручно. Але я все ще наражаюся на небезпеку отримати ритуальні поранення!

Ні, небезпеки в моєму житті й так достатньо.

@sfx Scraping1

Швиденько спускаюся, перебігаю дорогу та ховаюся під кущем.

@back Sc5_Outside2_1_TWOvasons

Ніч висовує мордочку з вікна. Не побачивши мене, роздратовано смикає хвостом і зникає в квартирі. Здається, вона мене облишила.

Але тривога мене не покидає. Тишком-нишком я швидко перебігаю вулицю і лізу під найближчі кущі.

@back Sc5_Outside3_inBushes

@choice "Коли ж припиниться це божевілля?" goto:.IWantBack
@choice "Де День?" goto:.WhereIsTheDay
@stop 

#WhereIsTheDay

    Якщо він і справді мій астральний тотем, то він єдиний, хто може мене врятувати.

    @goto .WatchingAtStrangers

#IWantBack

    Я хочу назад, в своє нормальне життя, до своїх нормальних котиків.

    @goto .WatchingAtStrangers

#WatchingAtStrangers

Якийсь час я так і сиджу під кущем, спостерігаючи за перехожими, що переходять вулицю або йдуть нею у справах.

@stopSfx HeartPalpitation

Це тиха вулиця, але іноді проїжджають автівки, і їхній шум лякає мене до напівсмерті.

@choice "Може, люди мені допоможуть?" goto:.PeopleWillHelpMe
@choice "Краще не висовуватися." goto:.KeepHeadDown
@stop

#PeopleWillHelpMe

    Повз мене проходить літня жінка з добрим обличчям. Я висовуюся з кущів.

    @choice "Допоможіть!" goto:.HelpMeWoman
    @choice "Сховатися назад." goto:.HideAway
    @stop

    #HelpMeWoman
        @sfx ManSteps
        
        @printer People
        Жінка: Що-що ти кажеш, котику-братику?

        @printer Dialogue
        {PlayerName}: Допоможіть.

        @choice "Моє життя ледь не обірвалося цієї ночі." goto:.ThisNight
        @choice "Мене перетворили на кота, але я людина!" goto:.IAmHuman
        @stop
        
        #ThisNight
            @printer Dialogue
            {PlayerName}: Допоможіть знайти вихід у реальний світ!
            @goto .WhyAreYouCrying

        #IAmHuman
            @printer Dialogue
            {PlayerName}: Розумієте? Я людина, перетворена на кота!
            @goto .WhyAreYouCrying
            
        #WhyAreYouCrying

            @sfx WomanSighs
            @printer People
            Жінка: Ну чого ти плачеш, маленький котику! В тебе такий тоненький голосочок.

            @printer Thoughts
            Її обличчя виглядає схвильовано.

            І здається, вона розуміє приблизно як банка консервів розуміє кота, який намагається загіпнотизувати її поглядом.

            @choice "Сховатися назад."
            @stop

            @printer People
            Жінка: Вибач, але в мене немає їжі. Я тобі завтра принесу трошки борщику!

            @printer Thoughts
            Літня жінка робить спробу мене погладити, але я ухиляюся.

            @printer People
            Жінка: Бувай, котику!
            
            @printer Thoughts
            Похитавши головою, жінка продовжує йти своєю дорогою.

            @goto .OnWindow

    #HideAway
        
        @printer Thoughts
        Жінка помічає мене, але не робить спроб контактувати. Просто продовжує йти своєю дорогою.
        @goto .OnWindow

#KeepHeadDown
    @printer Thoughts
    Я продовжую сидіти в кущах, тремтячи від кожного звуку.
    @goto .OnWindow

#OnWindow

@printer Thoughts
На підвіконня першого поверху виходить товстий сусідський кіт Жора.

@choice "Або потойбічна сутність, схожа на нього?" goto:.AfterChaosEnters
@choice "Або нащадок прибульців, який ним прикидається?" goto:.AfterChaosEnters
@stop

#AfterChaosEnters

Ким би він не був, він вмивається і виглядає цілком безпечно. 

Наскільки безпечно може виглядати рудий товстий кіт в кімоно, яке не сходиться на ньому.

@back Sc5_Outside2_1_TWOvasons
Перебігаючи дорогу, я все ще сподіваюся на те, що отримаю зрозумілі відповіді на свої питання…

А зблизька він зовсім не нагадує мені "торговця-рішалу"! Це геть інше створіння.

@sfx HelloMeow

@printer Chaos
@char Chaos.smile pos:75
Жора: Привіт, друже! Давай сюди!

@stopBgm Street fade:10
@bgm ChaosTheme
@printer Thoughts
@char Chaos.smile 
Мить — і ось ми вже разом, два коти на підвіконні першого поверху. 

Сусідка з третього поверху не любить вуличних котів, то ж я сподіваюся, що вона мене не помітить і трошки ховаюся за вазоном.

@printer Chaos
@char Chaos.def
Жора: Я знаю тебе! В тебе думки {PlayerName}, дуже розгублені думки.

@printer Dialogue
{PlayerName}: Щось типу того. А звідки ти знаєш?

@printer Chaos
@char Chaos.smile
Жора: Ти мені подобаєшся. Я все ще пам'ятаю смак консервів, які мені колись залишив {PlayerName}…

@printer Thoughts
Його мордочка стає мрійливою, а вуса розпушаються.

@printer Thoughts
То я намагався нагодувати бездомних собак, Жоро, але нехай...

@printer Chaos
@char Chaos.tricky
Жора: Що-що ти кажеш? Годував тих лякливих цуциків, що розбіглися, коли я підійшов? Ну добре…

@printer Dialogue
{PlayerName}: Нічого не кажу, це були мої думки. Ти вмієш їх читати?

@printer Chaos
@char Chaos.def
Жора: Я взагалі багато чого вмію. Тільки мало хто про те знає. Щодо думок, то вони не твої. Це не думки кота. В тобі чомусь сидять спогади людини.

@printer Dialogue
{PlayerName}: Дивно, правда?

@printer Dialogue
{PlayerName}: Мені треба тебе спитати, Жоро. Я не розумію, де реальність. 

{PlayerName}: Мій кіт розповідає мені, що я сплю й бачу сон, моя кішка запевняє, що мене перетворили на кота-прибульця.

@printer Chaos
@char Chaos.def
Жора: Давай не будемо поспішати, добре? До серйозних справ ще дійдемо. Спочатку дозволь представитись: мене звуть Хаос.

@choice "Знов ці ваші дивні імена." goto:.ThisNames
@choice "Дуже приємно." goto:.VeryPleasant
@stop

#ThisNames
    @printer Chaos
    Хаос: Так, ми віддаємо перевагу іменам, в яких є щось величне. Як тебе звуть, я знаю, {PlayerName}.

    @goto .HowDoYouKnow

#VeryPleasant
    @printer Chaos
    Хаос: Навзаєм. Хочеш собі те ім'я, серйозно? Ну добре. І знаю, яка в тебе пристрасть до того імені.

    @goto .HowDoYouKnow

#HowDoYouKnow
@printer Dialogue
{PlayerName}: Звідки ти це знаєш?

@printer Chaos
@char Chaos.tricky
Хаос: Слухай, а ти не допоможеш мені знайти щось поїсти? Не те, щоб я ходив голодним… Але, як ти пам'ятаєш, я загубив кльоцку.

@printer Chaos
Хаос: Я пішов до сусідки, яка дала мені ту кльоцку. Але вона подруга моєї хазяйки.

@printer Chaos
@char Chaos.def
Хаос: "Ні, котику, тобі треба худнути", — сказала вона.

@printer Chaos
Хаос: "І не вздумай сказати своїй хазяйці, що я тобі дала ту кльоцку!" — сказала вона.

@printer Chaos
@char Chaos.upset
Хаос: Але це не діло.

@printer Dialogue
{PlayerName}: Що не діло?

@printer Chaos
@char Chaos.smile
Хаос: Те, що я незадоволений! Адже сенс життя — в радості. Допоможеш мені знайти що-небудь смачненьке? А я розповім тобі, як бути з твоїми котами, Днем та Ніччю.

@printer Thoughts
Цей рудий кіт заглядає мені прямо в очі, нафуфиривши вуса та зацікавлено задерши хвіст.

@printer Chaos
Хаос: І не хвилюйся за вазон. Він давно вже їй не потрібен — вона поставила ту рослину засихати.

@printer Dialogue
{PlayerName}: Що?

@printer Thoughts
Раптом я розумію, що увесь цей час чіпляв своїм хвостом, який рухався без мого на те дозволу, щось важке. І воно повільно з'їжджало вниз.

@back Sc5_Outside2_2_ONEvason

@sfx BrickBreak

Ми з Хаосом дивимося вниз — там лежить понівечений вазон з рослиною, яку нарешті спіткала доля, омріяна хазяйкою.

@printer Chaos
@char Chaos.def
Хаос: Не хвилюйся, тобі кажу. Це не має жодного значення.

@printer Dialogue
{PlayerName}: Що?

@printer Thoughts
@char Chaos.sleepy
Хаос солодко потягується, відклячивши зад, потім переходить в асану "кішка мордою догори", розтяхуючи сухожилки задніх лап.

@sfx Yawn

@printer Chaos
@char Chaos.smile
Хаос:  Смачна їжа — ось що має значення! І доречі, їжа дуже мотивує мене на відверті розмови.

@printer Chaos
@char Chaos.upset
Хаос: В мене слабкі лапи, та ще й кігті обрізані. От якби хтось приніс мені мою кльоцку!

@printer Dialogue
{PlayerName}: Я принесу її, якщо ти даси відповіді на мої запитання!

@printer Chaos
@char Chaos.def
Хаос: Ну добре! Тільки давай швиденько…

@printer Dialogue
{PlayerName}: То мені вже можна ставити питання?

@printer Chaos
@char Chaos.tricky
Хаос: Так, валяй.

@printer Dialogue
{PlayerName}: Але спершу мені треба тобі дещо розповісти.

@printer Chaos
@char Chaos.smile
Хаос: Тільки пам'ятай, що ти віддаляєш поїдання делікатесної кльоцки з кожним словом!

@printer Thoughts
Я розповідаю йому все, що сталось зі мною цього ранку.

@printer Chaos
@char Chaos.def
Хаос: Мені здається, що ти чогось чекаєш. Може, наступного життя. Це нормально, якщо ти віриш, що в котів по дев'ять життів. 

Хаос: Але насправді ти маєш діяти в усій цій ситуації.

@printer Dialogue
{PlayerName}: В якій ситуації?

@printer Thoughts
@char Chaos.smile
Хаос здивовано розпушує вуса.

@printer Chaos
Хаос: Ти хіба не розумієш? Не треба мати надздібностей, щоб розуміти, що ти знаходишся в унікальному часопросторі. В тебе є купа можливостей, яких немає в жодної людини на світі, і в жодного кота.

@printer Chaos
Хаос: Ти можеш робити, що хочеш. 

Хаос: Можеш подорожувати зі своїм тотемом і вивчати астрал. Можеш урятувати світ від злочинця, підказавши людям, що коти розумні, а руденькі — найкращі.

@printer Chaos
@char Chaos.tricky
Хаос: Або можеш просто пограбувати зоомагазин, якщо докладеш зусиль.

@printer Chaos
Хаос: Особисто мені імпонує останній варіант. І якщо таки пограбуєш зоомагазин, не забудь поділитися котячими ковбасками зі своїм старим другом.

@printer Dialogue
{PlayerName}: Ніч каже, що ви прибульці. Але один з варіантів, який ти пропонуєш мені, — це повірити Дню. То де правда?

@printer Chaos
@char Chaos.def
Хаос: Поясню так, що правди не існує. Ти конструюєш її кожної миті в своєму мозку. Це ти обираєш, якій пропаганді вірити.

@printer Chaos
Хаос: Може, мої очі та вуха ведуть ворожу пропаганду, звідки мені знати. Я лише той, ким обираю бути.

@printer Chaos
@char Chaos.smile
Хаос: А я обираю бути Хаосом. То які твої запитання?

@set joker=0
@set whatsgoingon=0

@choice "То значить, правда в словах Ночі?" goto:.NightWords
@choice "Тоді правда в словах Дня?" goto:.DayWords
@stop

#NightWords

    @printer Dialogue
    {PlayerName}: І все ж таки ти прибулець?

    @printer Chaos
    @char Chaos.tricky
    Хаос: О так, я прибулець, син прибульця, і мені нічого не вартує контролювати твою волю.

    @printer Chaos
    @char Chaos.smile
    Хаос: То ж наказую тобі: піди й пограбуй для мене зоомагазин, і принеси мені всі котячі ковбаски, які ти там знайдеш!

    @printer Thoughts
    При словах "котячі ковбаски" зіниці Хаоса збільшуються, і це зовсім не по-прибульськи. Звичайний котик. Просто файно одягнений.

    @printer Chaos
    Хаос: Якщо що, дешеві смачніше.

    @printer Chaos
    @char Chaos.tricky
    Хаос: Так, я шпіон, друже — шпіон щастя, шпіон свійських розваг і післясмаку свята, яке з тобою кожен день!

    @goto .WhatsGoingOn

#DayWords

    @printer Dialogue
    {PlayerName}: То значить, я маю вірити Дню? Адже ти теж підіграєш Ночі, як і День, і говориш загадками.

    @printer Dialogue
    {PlayerName}: Скажи, насправді я в астральному світі серед духів? І ти теж — дух?

    @printer Chaos
    @char Chaos.tricky
    Хаос: О так, я дух — давні боги наказали мені прослідкувати за тобою, доки твій тотем з тобою не поруч. Я мушу оберігати тебе від примар, що підпали самоомані.

    @printer Chaos
    @char Chaos.smile
    Хаос: То ж, доки Ніч не зробила з тебе Сейлормун, наша місія полягає в тому, щоб постійно підтримувати баланс радості в моїй духовній ектоплазмі. 
    
    Хаос: Не хочеш пожартувати?

    @printer Dialogue
    {PlayerName}: Ти просто смієшся з мене.

    @printer Chaos
    @char Chaos.def
    Хаос: Ні.

    @goto .WhatsGoingOn

#WhatsGoingOn

@if joker==1&&whatsgoingon==1

    @goto .UnderstandOrNot

@else 

    @choice "То ти просто жартівник?" if:joker==0 goto:.YouAreJoker
    @choice "Ти хоч сам знаєш, що відбувається?" if:whatsgoingon==0 goto:.DoYouKnow
    @stop

    #YouAreJoker

        @set joker=joker+1

        @printer Chaos
        @char Chaos.smile
        Хаос: О ні, {PlayerName}! Я не жартую, я дуже серйозно з тобою говорю. 
        
        Хаос: Хіба що я і є той жарт, який викинула матінка-природа, коли обирала один з мільярдів варіантів буття.

        @goto .WhatsGoingOn

    #DoYouKnow

        @set whatsgoingon=whatsgoingon+1

        @printer Chaos
        @char Chaos.upset
        Хаос: В якому сенсі — що відбувається?

        @printer Dialogue
        {PlayerName}: Де істина? Кому вірити?

        @printer Thoughts
        Хаос неочікуванно підстрибує, перегортається в повітрі довкола своєї осі та приземлюється на тому ж місці.

        @printer Chaos
        @char Chaos.smile
        Хаос: Істина в смаколиках та котячій м'яті. А у що вірити, ми вирішуємо самі. Тут я тобі не помічник.

        @goto .WhatsGoingOn

@endIf

#UnderstandOrNot

@choice "Нічого не зрозуміло. Ти мене плутаєш." goto:.IDontUnderstand
@choice "О-о, я починаю розуміти..." goto:.IStartUnderstand
@stop

    #IDontUnderstand

        @printer Dialogue
        {PlayerName}: Навіщо ти намагаєшся мене ще більше заплутати?

        @printer Chaos
        @char Chaos.upset
        Хаос: Заплутати? О ні, я не люблю головоломки. Я обираю лише прості та азартні ігри. А те, що з тобою відбувається — це гарне питання.

        @choice "Тоді може ти на нього відповіси?" goto:.CanYouAnswer
        @choice "Може нам варто пограти?" goto:.CanWePlay
        @stop

        #CanYouAnswer

            @printer Dialogue
            {PlayerName}: Що це бляха таке?

            @printer Chaos
            @char Chaos.def
            Хаос: Це — вулиця, і ми на ній стоїмо.

            @printer Thoughts
            Хаос облизується, не витримує й додає:

            @printer Chaos
            @char Chaos.tricky
            Хаос: Мр-р-р, а це — дерево, і на ньому висить дещо смачненьке, то ж давай швидше розберемося з цим і ти дістанеш це для мене!

            @goto .IwantToTellYouSomething

        #CanWePlay
        
            @printer Chaos
            @char Chaos.def
            Хаос: Давай.

            @printer Thoughts
            Він підбирає два маленьких камінці

            @printer Dialogue
            {PlayerName}: чорний і білий.

            @printer Chaos
            Хаос: Вони однакової ваги. Кидаєш собі за спину та кажеш, який випаде ближче до тебе — чорний чи білий.

            @printer Chaos
            @char Chaos.tricky
            Хаос: Вгадаєш — викладу всю свою підноготну. Не вгадаєш — будеш розказувати свою.

            @sfx Stones

            @printer Thoughts
            Я зажимаю камінці в лапі, що для цього не пристосована, не з першого разу. І коли кидаю їх собі за спину, кажу…

            @choice "...білий." goto:.White
            @choice "...чорний." goto:.Black
            @stop 

            #Black

                @printer Chaos
                @char Chaos.tricky
                Хаос: Подивимося…

                @printer Thoughts
                Я обертаюся та бачу істину: ближче до мене лежить білий камінець.

                @printer Chaos
                @char Chaos.def
                Хаос: Ну, давай. Один секрет, який ще ніхто про тебе не знає.

                @printer Dialogue
                {PlayerName}: В мене… 
                @input PlayerSecret summary:"В мене "
                @stop

                @printer Chaos
                @char Chaos.upset
                Хаос: Оу!

                @printer Chaos
                Хаос: Не переймайся. Ніхто не дізнається, що в тебе {PlayerSecret}.

                @goto .IwantToTellYouSomething

            #White
                @printer Chaos
                @char Chaos.tricky
                Хаос: Подивимося…

                @printer Thoughts
                Я обертаюся та бачу білий камінець, який лежить в кроці від мене.

                @printer Dialogue
                {PlayerName}: Ура! Ну, що ти скажеш?

                @printer Chaos
                @char Chaos.upset
                Хаос: Новачкам щастить, ось що я скажу.

                @sfx TwoFriendlyMeow
                @toast "Перемогти Хаоса"

                @printer Chaos
                @char Chaos.def
                Хаос: А щодо мене, ну…

                @printer Chaos
                Хаос: Насправді я сам не розумію, що відбувається. Майже весь час я стирчу посеред чорного озера екзистенційної кризи.

                @printer Chaos
                @char Chaos.upset
                Хаос: Одного разу до мене дійшло, що все живе помирає. І з того моменту всі мої думки про смерть.

                @printer Chaos
                Хаос: Немає нічого важливішого за це, розумієш? В нас лише є час для того, щоб розважитися, доки ми не помремо.

                @printer Chaos
                Хаос: І найстрашніше: ми не знаємо, коли це станеться.

                @printer Chaos
                @char Chaos.smile
                Хаос: То ж яка різниця, чи існують всі ці роніксоні, шаманські тотеми в образі тварин, коти, люди?

                @printer Chaos
                Хаос: Просто живи кожен день як останній, і ні про що не переймайся. Окрім власної смерті.

                @sfx TwoFriendlyMeow
                @toast "Досягнення: Кіт, який полюбив смерть"

                @goto .IwantToTellYouSomething

#IStartUnderstand

    @printer Chaos
    @char Chaos.def
    Хаос: І що ти починаєш розуміти?

    @choice "Праксіологічну суб'єктивно-об'єктивно-темпоральну природу ідеосинкретичної реальності" goto:.Reality
    @choice "Квантову теорію поля, та побудову цього світу" goto:.QuantumWorld
    @choice "Швидкоплинність життя." goto:.TimeInLife
    @stop

    #Reality

        @toast "Досягнення: Божевільний Геній" 

        @printer Chaos
        @char Chaos.upset
        Хаос: Ой ні, я не розумію тих слів. Надто складні матерії для мене. Тобі краще поговорити про це з кимось іншим.
        
        @goto .IwantToTellYouSomething

    #QuantumWorld

        @toast "Досягнення: Кіт Ейнштейна" 
    
        @printer Chaos
        @char Chaos.upset
        Хаос: Ой ні, я не розумію тих слів. Надто складні матерії для мене. Не певен, що хоч хтось в світі її розуміє...

        @goto .IwantToTellYouSomething

    #TimeInLife

        @toast "Досягнення: Кіт-буддист" 

        @printer Chaos
        @char Chaos.smile
        Хаос: О, це добре. Чесно кажучи, насправді я сам не розумію, що відбувається. Майже весь час я стирчу посеред чорного озера екзистенційної кризи.

        @printer Chaos
        Хаос: Немає нічого важливішого за життя, розумієш? В нас є час лише для того, щоб розважитися, поки ми живі.

        @printer Chaos
        Хаос: Ми не знаємо, коли помремо. То ж яка різниця, чи існують всі ці роніксоні, шаманські тотеми в образі тварин, коти, люди?

        @printer Chaos
        Хаос: Просто живи кожен день як останній, і ні про що не переймайся. Роби те, що підказує тобі серце.

        @goto .IwantToTellYouSomething

#IwantToTellYouSomething

@printer Chaos
@char Chaos.def
Хаос: Я хочу донести до тебе дещо. І не тому, що я такий добрий. Лише на знак вдячності хочу тобі сказати.

@printer Dialogue
{PlayerName}: Ну не томи вже…

@printer Chaos
@char Chaos.tricky
Хаос: Моя хазяйка каже, що якщо поставити каву на сильний вогонь, вона втратить смак. Треба саме томити. Тож…

@printer Thoughts
Мабуть, мій погляд чимось нагадував асфальтоукладальний каток, тому що Хаос починає говорити швидше…

@printer Chaos
@char Chaos.def
Хаос: Я хочу донести до тебе: реальності безвідносно твоєї волі не існує. 

Хаос: Нікому не вір, або вір всім одразу. Життя одне, і в ньому немає нічого непосильного для розумної істоти. 

Хаос: Це означає, що правда буде там, де ти її поставиш.

@printer Thoughts
…але сенс його слів не змінюється. Здається, він говорить щиро.

@printer Chaos
@char Chaos.def
Хаос: Ти маєш щось вибрати, а потім зняти з дерева кльоцку й віддати мені. То що ти вибереш, котику-братику?

@choice "Допоможу врятувати людство" goto:.RealityChange
@choice "Піду закончу навчання урокам свого тотема" goto:.RealityChange
@choice "Піду пограбую зоомагазин" goto:.RealityChange
@choice "Залізу на дерево й буду нявчати, доки мене не знімуть" goto:.RealityChange
@stop

#RealityChange

@stopBgm ChaosTheme fade:10
@bgm Street fade:10
@printer Thoughts
Тільки я відкриваю рота, щоб щось сказати, як реальність знову змінюється.

@sfx SingingBirdie

З крони дерева виринає пташка, і стає центром всесвіта, летючим омфалом, сяючим серцем темряви.

Я все ще пам'ятаю про розмову з Хаосом, але простір вже викривився. Все, що мені залишається сказати — це…

@choice "Кя-кя-кя!" goto:.Birdie
@choice "Кє-кє-кє!" goto:.Birdie
@stop 

#Birdie

@printer Chaos
Хаос: От і добре! Гарний котик. А я піду проведу цю сієсту в своєму домику…

@printer Thoughts
Інтуїтивно я розумію, що пташка скоріше до мене підлетить, якщо я буду маленьким і тонкоголосим.

Я входжу в режим стелс, стаю шпіоном в пташиному світі від світу котячого.

Пташка трохи порхає вздовж вулиці, а я її переслідую. Аж раптом…

@hideChars
@goto Scene6
